<!DOCTYPE html>
<html lang="eu">

  <head>
  <meta charset="utf-8">
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1">

  <meta name="description" content="Examples for sgl">
  <meta name="author" content="stisa">
  <link rel="stylesheet" href="http://stisa.space/css/main.css">
  <link rel="stylesheet" href="http://stisa.space/css/prism.css">
  <link rel="apple-touch-icon-precomposed" href="http://stisa.space/favicon-152.png">
  <meta name="msapplication-TileColor" content="#FFFFFF">
  <meta name="msapplication-TileImage" content="http://stisa.space/favicon-144.png">
  <style>@media only screen and (min-width: 800px)  { .output { float:left; padding-right:1em;} } </style>
  <title>Example: squares</title>  
</head>
<body>
  <header class="sidebar">

    <h3>Menu</h3>

    <div class="menu">

      <a href="/">Home</a>
      <a href="/projects/">Projects</a>
      <a href="/about/">About Me</a>
      <br/>
      <a href="/sgl/">sgl</a>
    </div>
  </header>
  <section class="content">
    <section class="output" id="output">
      <!--TODO:meaningful output-->
      <h2>Example: squares</h2>
      <canvas style="width:100%; height:100%; min-height:25em;" width="640" height="480" id="sgl-canvas">
        Your browser doesn't appear to support the 
        <code>&lt;canvas&gt;</code> element.
      </canvas>
      <script type="text/javascript" src="./squares.js"></script>
    </section>
    <section class="examplecode">
      <h2>Code</h2>
      <pre><code class="language-nim">import webgl, dom,math,jsconsole
import ../src/sgl/[state,utils]

let 
  # Vertex shader source code
  vertCode = """
  uniform mat3 translation;
  uniform mat3 projection;
  uniform mat3 rotation;
  attribute vec2 coordinates;
  void main(void) {
    gl_Position = vec4( vec3( projection * translation * rotation * vec3(coordinates,1.0) ).xy, 0.0 , 1.0);
  }
  """
  #fragment shader source code
  fragCode ="""
  precision mediump float;
  uniform vec4 u_color;
  void main(void) {
    gl_FragColor = u_color;
  }
  """

  vertices = [
    0.0,0,
    100,0,
    100,100
  ];

var 
  lasttranslation = translation3(0,0)
  lastrotation = rotation3(0.0)
  indices = [0'u16,1,2]

  sgl = initstate("sgl-canvas",vertcode,fragCode)

sgl["projection"] = projection3(sgl.gl.drawingbufferwidth.float,sgl.gl.drawingbufferheight.float)
sgl["translation"] = lasttranslation # set the uniform "translation"
sgl["rotation"] = rotation3(0) # set the uniform "translation"
sgl["u_color"] = [0.0,0,0,1] # set the uniform "u_color"

sgl.upload(vertices,indices)
sgl.point("coordinates") # Tell webgl that the buffers we just uploaded are "coordinates"

var
  dx,dy,theta = 0.0

  last_time = -1.0
  dt = 0.0

setupFPSCounter("output")

proc draw() =
  # Update translation uniforms
  if translation3(dx,dy) != lasttranslation:
    sgl["translation"] = translation3(dx,dy)
    lasttranslation = translation3(dx,dy)
  if rotation3(theta) != lastrotation:
    sgl["rotation"] = rotation3(theta)
    lastrotation = rotation3(theta)
  # Draw the triangle
  sgl.drawElementsAs(pmTriangles)

proc update(then:float) =
  # Main Loop
  requestAnimationFrame(update)

  dt = then - last_time

  # Limit to 30fps
  if dt>1000/30:  
    # FPS counter
    updateFpsCounter(dt)
    draw()
    last_time = then

# Example binding keyboard input
proc keyev(e:dom.Event) =
  if (char(e.keycode)) == 'a':
    dx-=30 
  elif (char(e.keycode)) == 'd':
    dx+=30 
  elif (char(e.keycode)) == 'w':
    dy+=30
  elif (char(e.keycode)) == 's':
    dy-=30
  elif (char(e.keycode)) == 'q':
    theta-=5
  elif (char(e.keycode)) == 'e':
    theta+=5
document.addEventlistener("keypress",keyev,true)

# Start looping
update(0.0)

</code></pre>
    </section>
  </section>
  <script src="http://stisa.space/js/prism.js"></script>
  
</body>
